
dnl -----------------------------------------------------------------------
dnl Stage Autoconf script
dnl $Id: configure.in,v 1.25 2002-11-03 00:14:34 gerkey Exp $
dnl -----------------------------------------------------------------------

dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/main.cc)

dnl determine system type.  this is used in a couple of places to infer the 
dnl types of arguments to some networking library functions.  if/when we 
dnl upgrade to newer Autotools, this can probably go away.
AC_CANONICAL_SYSTEM
case $target in
*-*-solaris*) AC_DEFINE(PLAYER_SOLARIS,1,[building for Solaris]);;
*-*-linux*) AC_DEFINE(PLAYER_LINUX,1,[building for Linux]);;
esac


dnl Need AC_PROG_MAKE_SET for recursive building
AC_PROG_MAKE_SET

dnl Generate a configuration header called <config.h> and put all the C
dnl preprocessor defines in there
AM_CONFIG_HEADER(config.h)

dnl Define this so that we can refer to it twice below
define(STAGE_VERSION,1.3)

dnl Initialize automake with name and version of package to be built. 
dnl PACKAGE and VERSION will be so defined in <config.h>.
AM_INIT_AUTOMAKE(stage,STAGE_VERSION)

dnl Change default installation prefix from /usr/local to user's home
AC_PREFIX_DEFAULT(${HOME}/stage-STAGE_VERSION)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_INSTALL
AC_PROG_RANLIB

dnl Checks for header files.
AC_CHECK_HEADERS(stdint.h sys/types.h strings.h values.h)
#AC_HEADER_STDC
#AC_HEADER_SYS_WAIT
#AC_CHECK_HEADERS(fcntl.h limits.h sys/file.h sys/time.h unistd.h)

dnl RTK2 uses libjpeg to export images.
#AC_CHECK_HEADER(jpeglib.h,
  #AC_DEFINE(HAVE_JPEGLIB_H,1,[include jpeg support])
  #LIBJPEG="-ljpeg",,)
#AC_SUBST(LIBJPEG)

dnl Checks for typedefs, structures, and compiler characteristics.
#AC_C_CONST
#AC_TYPE_OFF_T
#AC_TYPE_SIZE_T
#AC_HEADER_TIME

dnl Checks for library functions.
AC_REPLACE_FUNCS(poll dirname)
AC_CHECK_LIB(z, gzopen)
AC_CHECK_LIB(nsl, gethostbyname)
AC_CHECK_LIB(socket, connect)
AC_CHECK_LIB(rt, sem_wait)
AC_CHECK_LIB(posix4, sem_wait)
AC_CHECK_FUNCS(inet_aton srand48 lrand48)
#AC_FUNC_MMAP
#AC_FUNC_SETPGRP
#AC_TYPE_SIGNAL
#AC_CHECK_FUNCS(getcwd gethostname gettimeofday mkdir rmdir select socket strcspn strdup strerror)

dnl if the GNOME2 GUI was requested, we use that
AC_ARG_WITH(gnome2,    [   --with-gnome2         Compile in the experimental GNOME2 GUI instead of RTK2],,with_gnome2=no)
if test "x$with_gnome2" = "xyes"; then
  AC_DEFINE(USE_GNOME2,1,[include the GNOME2 GUI])
  GUI_DIR=gnome2
  GUI_LIB=libgnomestage.a
  GUI_LINK="-Lgnome2 -lgnomestage `pkg-config --libs libgnomecanvas-2.0 libgnomeui-2.0`"
else
  dnl otherwise we use the RTK GUI unless it is explicitly refused
  dnl where's RTK?
  AC_ARG_WITH(rtk, [   --with-rtk=dir      Location of RTK],
  RTK_DIR=$with_rtk,
  RTK_DIR="${HOME}/rtk-2.1")
  AC_SUBST(RTK_DIR)

  dnl RTK2 uses libjpeg to export images.
  AC_CHECK_HEADER(jpeglib.h,
    AC_DEFINE(HAVE_JPEGLIB_H,1,[include jpeg support])
    LIBJPEG="-ljpeg",,)

  if test -f $RTK_DIR/include/rtk.h; then
    with_rtk=yes
    AC_DEFINE(INCLUDE_RTK2,1,[include the RTK GUI])
    GUI_DIR=rtkstage
    GUI_LIB=librtkstage.a
    GUI_LINK="-L$RTK_DIR/lib -Lrtkstage -lrtkstage -lrtk $LIBJPEG `gtk-config --libs gtk gthread`"
  else
    with_rtk=no
  fi
  AM_CONDITIONAL(WITH_RTK, test x$with_rtk = xyes)
fi

# define the paths, libs and deps for the GUI
AC_SUBST(GUI_LINK)
AC_SUBST(GUI_DIR)
AC_SUBST(GUI_LIB)

# set vars depending on the GUI tests
AM_CONDITIONAL(WITH_GNOME2, test x$with_gnome2 = xyes)
AM_CONDITIONAL(WITH_GUI, test x$with_gui = xyes)


dnl where's player?
AC_ARG_WITH(player, [   --with-player=dir      Location of Player (installation or source)],
PLAYER_DIR=$with_player,
PLAYER_DIR="${HOME}/player-STAGE_VERSION")
if test ! -f $PLAYER_DIR/include/player.h; then
  if test ! -f $PLAYER_DIR/server/player.h; then
    AC_MSG_ERROR("Could not find player.h.  Player is required to build Stage.")
  fi
fi
AC_SUBST(PLAYER_DIR)

dnl where's the X11-style color database?
AC_CHECK_FILE([/usr/X11R6/lib/X11/rgb.txt],
  CDB="\"/usr/X11R6/lib/X11/rgb.txt\"",
  AC_CHECK_FILE([/usr/openwin/lib/X11/rgb.txt],
    CDB="\"/usr/openwin/lib/X11/rgb.txt\"",
    CDB="\"/usr/X11R6/lib/X11/rgb.txt\""))
AC_ARG_WITH(color, [   --with-color=file      Location of X11-style color file],
CDB="\"$with_color\"",)
AC_DEFINE_UNQUOTED(COLOR_DATABASE,$CDB,[the X11-style color database])

dnl should tests (e.g., RTK) be built?
AC_ARG_WITH(tests, [  --with-tests            Build tests],,
with_tests=yes)
AM_CONDITIONAL(WITH_TESTS, test x$with_tests = xyes)

AC_OUTPUT(Makefile 
          src/Makefile 
	  src/models/Makefile	
          src/gnome2/Makefile 
	  src/rtkstage/Makefile
          worlds/Makefile 
          tools/Makefile)
